# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <blacksky3@tuta.io> <https://github.com/blacksky3>
#Credits: Giancarlo Razzolini <grazzolini@archlinux.org>
#Credits: Frederik Schwan <freswa at archlinux dot org>
#Credits:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
#Credits: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->glibc->binutils->gcc

pkgname=binutils-blacksky
pkgver=2.38
pkgrel=1
pkgdesc='A set of programs to assemble and manipulate binary and object files'
arch=(x86_64)
url='https://www.gnu.org/software/binutils/'
license=(GPL3 GPL FDL1.3 custom:FSFAP)
groups=(base-devel)
depends=(glibc zlib libelf)
checkdepends=(dejagnu debuginfod bc)
optdepends=('debuginfod: for debuginfod server/client functionality')
provides=(binutils "binutils=${pkgver}" binutils-blacksky "binutils-blacksky=${pkgver}")
conflicts=(binutils-multilib binutils)
replaces=(binutils-multilib)
options=(staticlibs !distcc !ccache !debug)
source=(https://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.xz
        gold-warn-unsupported.patch)

sha256sums=(e316477a914f567eccc34d5d29785b8b0f5a10208d36bbacedcc39048ecfe024
            2d430b66f84a19c154725ec535280c493be1d34f1a90f95208dce02fecd0e4e4)

prepare(){
  [[ ! -d binutils-gdb ]] && ln -s binutils-$pkgver binutils-gdb
  mkdir -p binutils-build

  cd ${srcdir}/binutils-gdb

  # Turn off development mode (-Werror, gas run-time checks, date in sonames)
  sed -i '/^development=/s/true/false/' bfd/development.sh

  # Creds @Fedora
  # Change the gold configuration script to only warn about
  # unsupported targets.  This allows the binutils to be built with
  # BPF support enabled.
  patch -Np1 -i "${srcdir}"/gold-warn-unsupported.patch
}

build(){
  cd ${srcdir}/binutils-build

  "$srcdir/binutils-gdb/configure" \
    --prefix=/usr \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --enable-cet \
    --enable-deterministic-archives \
    --enable-gold \
    --enable-install-libiberty \
    --enable-ld=default \
    --enable-pgo-build=lto \
    --enable-plugins \
    --enable-relro \
    --enable-shared \
    --enable-targets=x86_64-pep,bpf-unknown-none \
    --enable-threads \
    --disable-gdb \
    --disable-gdbserver \
    --disable-libdecnumber \
    --disable-readline \
    --disable-sim \
    --disable-werror \
    --with-debuginfod \
    --with-pic \
    --with-system-zlib

  make -j$(nproc) -O tooldir=/usr

  # Extract the FSF All Permissive License
  # <https://www.gnu.org/prep/maintain/html_node/License-Notices-for-Other-Files.html>
  # used for some linker scripts.
  tail -n 5 "${srcdir}"/binutils-${pkgver}/ld/scripttempl/README > FSFAP
}

check(){
  cd ${srcdir}/binutils-build

  # Use minimal flags for testsuite
  # ld testsuite uses CFLAGS_FOR_TARGET and requires -g
  # gold testsuite requires CXXFLAGS/CFLAGS with default PIE/PIC disabled
  make -j$(nproc) -O CFLAGS_FOR_TARGET="-O2 -g" \
                     CXXFLAGS="-O2 -no-pie -fno-PIC" \
                     CFLAGS="-O2 -no-pie" \
                     LDFLAGS="" \
                     check
}

package(){
  cd ${srcdir}/binutils-build
  make -j$(nproc) prefix="$pkgdir/usr" tooldir="$pkgdir/usr" install

  # install PIC version of libiberty
  install -m644 libiberty/pic/libiberty.a "$pkgdir/usr/lib"

  # Remove unwanted files
  rm -f "$pkgdir"/usr/share/man/man1/{dlltool,windres,windmc}*

  # No shared linking to these files outside binutils
  rm -f "$pkgdir"/usr/lib/lib{bfd,opcodes}.so
  echo 'INPUT( /usr/lib/libbfd.a -liberty -lz -ldl )' > "$pkgdir/usr/lib/libbfd.so"
  echo 'INPUT( /usr/lib/libopcodes.a -lbfd )' > "$pkgdir/usr/lib/libopcodes.so"

  # install FSF All Permissive License
  install -Dm644 -t "${pkgdir}"/usr/share/licenses/${pkgname}/ FSFAP
}
