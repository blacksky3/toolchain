# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <blacksky3@tuta.io> <https://github.com/blacksky3>
#Credits:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
#Credits: Allan McRae <allan@archlinux.org>
#Credits: judd <jvinet@zeroflux.org>

# NOTE: requires rebuilt with each new gcc version

pkgname=libtool-git
pkgver=2.4.6.r63.gf5eb6f11
pkgrel=1
_gccver=11.2.0
pkgdesc='A generic library support script (git version)'
arch=(x86_64)
url='https://www.gnu.org/software/libtool'
license=(GPL)
groups=(base-devel)
depends=(sh tar glibc)
makedepends=("gcc>=$_gccver" git help2man)
checkdepends=(gcc-fortran)
provides=("libltdl=$pkgver" "libtool-multilib=$pkgver" libtool libltdl-git libtool-multilib-git "libltdl-git=${pkgver}" "libtool-multilib-git=${pkgver}")
conflicts=(libltdl libtool-multilib libtool)
replaces=(libltdl libtool-multilib)
source=(git+https://git.savannah.gnu.org/git/libtool.git
        git+https://git.savannah.gnu.org/git/gnulib.git
        gnulib-bootstrap::git+https://github.com/gnulib-modules/bootstrap.git
        no_hostname.patch
        disable-lto-link-order2.patch)
md5sums=(SKIP
         SKIP
         SKIP
         63326a457c32c404e630c1c9a4603acb  #no_hostname.patch
         5ce7876a865f3b981434e03ea0b9a54e) #disable-lto-link-order2.patch

pkgver(){
  cd ${srcdir}/libtool

  # cutting off 'foo-' prefix that presents in the git tag
  #git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'

  #git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
  #git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cd ${srcdir}/libtool

  patch -Np1 -i ${srcdir}/no_hostname.patch

  # test 67 is broken with lto
  # this patch removes the -flto flag for this very test
  # adapt when -ffat-lto-objects is enabled by Arch
  patch -Np1 -i ${srcdir}/disable-lto-link-order2.patch

  git submodule init
  git config --local submodule.gnulib.url "$srcdir/gnulib"
  git config --local submodule.gl-mod/bootstrap.url "$srcdir/gnulib-bootstrap"
  git submodule update

  ./bootstrap
}

build(){
  cd ${srcdir}/libtool
  ./configure --prefix=/usr lt_cv_sys_lib_dlsearch_path_spec="/usr/lib /usr/lib32"
  make -j$(nproc)
}

check() {
  cd ${srcdir}/libtool
  make -j$(nproc) check gl_public_submodule_commit=
}

package(){
  cd ${srcdir}/libtool
  make -j$(nproc) DESTDIR="$pkgdir" install
}
